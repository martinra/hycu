## todo: name executables: hycu-mpi etc.

Import(["env", "env_mpi"])


sources_curve = [
    "block_iterator.cc"
  , "curve.cc"
  , "curve_iterator.cc"
  , "fq_element_table.cc"
  , "reduction_table.cc"
  , "single_curve_fp.cc"
  ]
if env["with_opencl"]:
  sources_curve += [
      "opencl/interface.cc"
    , "opencl/buffer_evaluation.cc"
    , "opencl/kernel_evaluation.cc"
    , "opencl/kernel_reduction.cc"
    , "opencl/program.cc"
    , "opencl/program_evaluation.cc"
    , "opencl/program_reduction.cc"
    ]

objs_curve = [ env.Object(s) for s in sources_curve ]
Export("objs_curve")


sources_config = [
    "config/config_node.cc"
  ]
objs_config = [ env.Object(s) for s in sources_config ]
Export("objs_config")


sources_store = [
    "store/curve_data.cc"
  , "store/file_store.cc"
  , "store/store.cc"
  , "store/store_data.cc"
  ]
objs_store = [ env.Object(s) for s in sources_store ]
Export("objs_store")

sources_standalone_worker_pool = [
    "worker_pool/standalone.cc"
  ]
objs_standalone_worker_pool = \
   [ env.Object(s) for s in sources_standalone_worker_pool ]
Export("objs_standalone_worker_pool")

sources_threaded = [
    "threaded/thread.cc"
  , "threaded/thread_pool.cc"
  ]
objs_threaded = [ env.Object(s) for s in sources_threaded ]
Export("objs_threaded")


prg_single = env.Program(
    "executables/hycu-single"
  , [ "executables/single.cc" ]
    + objs_curve
  )
inst_single = env.InstallAs(env["prefix"] + "/bin/hycu-single", prg_single)
Alias("single", inst_single)


prg_threaded = env.Program(
    "executables/hycu-threaded"
  , [ "executables/threaded.cc" ]
    + objs_curve
    + objs_config
    + objs_store
    + objs_threaded
    + objs_standalone_worker_pool
  )
inst_threaded = env.InstallAs(env["prefix"] + "/bin/hycu-threaded", prg_threaded)
Alias("threaded", inst_threaded)


prg_mpi = env_mpi.Program(
    "executables/hycu-mpi"
  , [ "executables/mpi.cc"
    , "worker_pool/mpi.cc"
    , "worker_pool/mpi_worker.cc"
    ]
    + objs_curve
    + objs_config
    + objs_store
    + objs_threaded
  )
inst_mpi = env_mpi.InstallAs(env["prefix"] + "/bin/hycu-mpi", prg_mpi)
Alias("mpi", inst_mpi)


prg_merger = env.Program(
    "executables/hycu-merger"
  , [ "executables/merger.cc" ]
    + objs_curve
    + objs_store
  )
inst_merger = env.InstallAs(env["prefix"] + "/bin/hycu-merger", prg_merger)
Alias("merger", inst_merger)
