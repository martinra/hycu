import os

Import("root_dir")


include_path = [
    root_dir + "/src"
  ]

lib_path = [
    "/usr/local/lib"
  ]

lib_path_opencl = [
    "/usr/lib64/beignet"
  , "/usr/lib/x86_64-linux-gnu"
  ]

libs = [
    "cl"
  , "flint"
  , "gmp"
  , "yaml-cpp"
  ]

env = Environment(
    CXXFLAGS = "-std=c++14 -O2 -pthread"
  , CPPPATH  = include_path
  , LIBPATH  = lib_path + lib_path_opencl
  , LIBS     = libs
  )

Export("env")


sources_curve = [
    "block_iterator.cc"
  , "curve.cc"
  , "curve_iterator.cc"
  , "fq_element_table.cc"
  , "isogeny_representative_store.cc"
  , "opencl_interface.cc"
  , "reduction_table.cc"
  , "single_curve_fp.cc"
  ]

objs_curve = [ env.Object(s) for s in sources_curve ]
Export("objs_curve")


env.Program(
    "single"
  , [ "single.cc" ] + objs_curve
  )

env.Program(
    "legacy"
  , [ "legacy.cc", "isogeny_count_store.cc" ] + objs_curve
  )


##############################
## merge_isogeny_representative
##############################

libs_merge_isogeny_representatives = [
    "boost_filesystem"
  , "boost_system"
  ]

env_merge_isogeny_representatives = env.Clone()
env_merge_isogeny_representatives.Append(
    LIBS = libs_merge_isogeny_representatives
  )

env_merge_isogeny_representatives.Program(
    "merge_isogeny_representatives"
  , [ "merge_isogeny_representatives.cc" ]
    + objs_curve
  )


##############################
## mpi
##############################


libs_mpi = [
    "boost_mpi"
  , "boost_serialization"
  ]

env_mpi = env.Clone()
env_mpi.Append(
  ENV = {"PATH": os.environ.get("PATH","")}
  )
env_mpi.ParseConfig("mpic++ --showme:compile")
env_mpi.ParseConfig("mpic++ --showme:link")
env_mpi.Append(
    LIBS       = libs_mpi
  )


env_mpi.Program(
    "mpi"
  , [ "mpi.cc" , "mpi_worker_pool.cc" ]
    + objs_curve
  )
